# Copyright (c) 2018 Jakob Meng, <jakobmeng@web.de>
#
# Redistribution and use is allowed according to the terms of the BSD license.
# For details see the accompanying COPYING-CMAKE-SCRIPTS file.
#

@PACKAGE_INIT@

set_and_check(hbrs-theta_utils_BINDIR      "@PACKAGE_HBRS_THETA_UTILS_INSTALL_BINDIR@")
set_and_check(hbrs-theta_utils_LIBDIR      "@PACKAGE_HBRS_THETA_UTILS_INSTALL_LIBDIR@")
set_and_check(hbrs-theta_utils_INCLUDEDIR  "@PACKAGE_HBRS_THETA_UTILS_INSTALL_INCLUDEDIR@")
set_and_check(hbrs-theta_utils_CMAKEDIR    "@PACKAGE_HBRS_THETA_UTILS_INSTALL_CMAKEDIR@")

check_required_components(hbrs-theta_utils)

# NOTE: Keep list of dependencies in sync with packages in CMakeLists.txt!
include(CMakeFindDependencyMacro)
# NOTE: hbrs-cmake is only used during build
#if(@hbrs-cmake_FOUND@)
#    find_dependency(hbrs-cmake REQUIRED)
#endif()
if(@hbrs-mpl_FOUND@)
    find_dependency(hbrs-mpl REQUIRED)
endif()
if(@Boost_FOUND@)
    find_dependency(Boost 1.62 REQUIRED COMPONENTS unit_test_framework system thread serialization chrono program_options filesystem iostreams regex exception signals)
endif()
if(@MPI_FOUND@)
    find_dependency(MPI)
endif()
if(@netcdf_FOUND@)
    find_dependency(netcdf)
endif()
if(@VTK_FOUND@)
    find_dependency(VTK)
endif()



set(hbrs-theta_utils_CXX_COMPILER_ID "@CMAKE_CXX_COMPILER_ID@")
set(hbrs-theta_utils_CXX_COMPILER_VERSION "@CMAKE_CXX_COMPILER_VERSION@")

if((NOT hbrs-theta_utils_CXX_COMPILER_ID STREQUAL CMAKE_CXX_COMPILER_ID) OR
   (NOT hbrs-theta_utils_CXX_COMPILER_VERSION STREQUAL CMAKE_CXX_COMPILER_VERSION))
    message(WARNING 
    "This code was built with C++ compiler ${hbrs-theta_utils_CXX_COMPILER_ID} ${hbrs-theta_utils_CXX_COMPILER_VERSION}, "
    "but you're using ${CMAKE_CXX_COMPILER_ID} ${CMAKE_CXX_COMPILER_VERSION}. "
    "This might cause linking problems or hardly traceable errors at runtime due to binary incompatibilities! "
    "Better use the same compiler and same compiler version. For details see "
    "https://community.kde.org/Policies/Binary_Compatibility_Issues_With_C++")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/hbrs-theta_utils-targets.cmake")
